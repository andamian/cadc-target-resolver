plugins {
    id 'idea'
    id 'war'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8
group = 'org.opencadc'
version = '1018'

war {
    archiveName project.name + '##' + project.version + '.war'
}

sourceSets {
    intTest
}

configurations {
    intTestImplementation.extendsFrom(testImplementation)
}

idea {
    module {
        // Add some extra test source dirs
        testSourceDirs += file('src/intTest/java')
        scopes.TEST.plus += [ configurations.intTestImplementation ]
    }
}

dependencies {
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.json:json:20210307'
    implementation 'org.jdom:jdom2:[2.0.6,3.0)'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    implementation 'org.opencadc:cadc-dali:[1.2.4,)'
    implementation 'org.opencadc:cadc-util:[1.2.25,)'
    implementation 'org.opencadc:cadc-log:[1.1.0,)'
    implementation 'org.opencadc:cadc-vosi:[1.3.1,)'
    implementation 'org.opencadc:caom2-search-lib:[2.5.2, 3.0)'

    runtimeOnly 'javax.servlet.jsp.jstl:jstl-api:1.2'
    runtimeOnly 'javax.servlet:jstl:1.2'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.skyscreamer:jsonassert:[1.5.0,)'
    testImplementation 'org.xmlunit:xmlunit-core:[2.6.0,3.0)'
    testImplementation 'org.easymock:easymock:[3.4,4.0)'
    testImplementation 'org.opencadc:cadc-registry:[1.0.3,)'

    intTestImplementation 'org.opencadc:cadc-test-vosi:[1.0.2,)'
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}
