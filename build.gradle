plugins {
    id 'maven'
    id 'idea'
    id 'war'
}

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.7
group = 'org.opencadc'
version = '1017'

war {
    archiveName = project.name + '##' + project.version + '.war'
}

sourceSets {
    intTest
}

configurations {
    intTestCompile.extendsFrom(testCompile)
    intTestRuntime.extendsFrom(testRuntime)
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/intTest/java')
        scopes.TEST.plus += [ configurations.intTestCompile ]
    }
}

dependencies {
    compile 'log4j:log4j:1.2.+'
    compile 'org.json:json:20180130'
    compile 'org.jdom:jdom2:2.+'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.+'

    compile 'org.opencadc:cadc-dali:1.+'
    compile 'org.opencadc:cadc-util:[1.2.25,)'
    compile 'org.opencadc:cadc-log:[1.1.0,)'
    compile 'org.opencadc:cadc-vosi:[1.3.2,)'
    compile 'org.opencadc:caom2-search-lib:[2.3.1,)'

    runtime 'jstl:jstl:1.2+'

    testCompile 'junit:junit:4.+'
    testCompile 'org.skyscreamer:jsonassert:1.+'
    testCompile 'org.xmlunit:xmlunit-core:2.+'
    testCompile 'org.easymock:easymock:3.+'
    testCompile 'org.opencadc:cadc-registry:[1.0.3,)'

    intTestCompile 'org.opencadc:cadc-test-vosi:[1.0.2,)'
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}
